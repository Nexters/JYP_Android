plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

Properties localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())

android {
    namespace 'com.jyp.feature_sign_in'
    compileSdk project.compileSdkVersion.toInteger()

    defaultConfig {
        minSdk 23
        targetSdk project.targetSdkVersion.toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "KAKAO_NATIVE_APP_KEY", localProperties['kakao_native_app_key']
    }

    buildTypes {
        debug {
            manifestPlaceholders = [
                    KAKAO_NATIVE_APP_KEY: localProperties['kakao_native_app_key']
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [
                    KAKAO_NATIVE_APP_KEY: localProperties['kakao_native_app_key']
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.compose_version
    }
}

dependencies {
    implementation project(':core_ui')
    implementation project(':core_network')
    implementation project(':feature_main')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation composeDependencies.implementation.values()
    debugImplementation composeDependencies.debugImplementation.values()

    implementation hiltDependencies.implementation.values()
    kapt hiltDependencies.kapt.values()

    implementation networkDependencies.implementation.values()
    implementation kakaoSignInDependencies.implementation.values()
}